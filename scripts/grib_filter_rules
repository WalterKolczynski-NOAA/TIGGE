# This filter should only be run on GRIB edition 2; abort otherwise
assert( edition == 2 );
#print " START ";
set productionStatusOfProcessedData = 4;
set subdivisionsOfBasicAngle = missing();
set localTablesVersion = 0;
set tablesVersion = 5;
set numberOfForecastsInEnsemble = numberOfForecastsInEnsemble + 1;

if(parameterCategory == 0 && parameterNumber == 5 && typeOfFirstFixedSurface == 103 && discipline == 0)
	{ set tigge_short_name = 'mn2t6'; }

if(parameterCategory == 0 && parameterNumber == 4 && typeOfFirstFixedSurface == 103 && discipline == 0)
	{ set tigge_short_name = 'mx2t6'; }

if(parameterCategory == 6 && parameterNumber == 1 && typeOfFirstFixedSurface == 200 && discipline == 0)
	{ 
	#set productDefinitionTemplateNumber = 1;
	set tigge_short_name = 'tcc';
	}

if(parameterCategory == 3 && parameterNumber == 1 && typeOfFirstFixedSurface == 101 && discipline == 0)
	{ set tigge_short_name = 'msl'; }

#  CAPE
if( parameterCategory == 7 && parameterNumber == 6 && discipline == 0 && 
    typeOfFirstFixedSurface == 1 && typeOfSecondFixedSurface == 255  )
	{ 
#	set tigge_short_name = 'cape';
	set parameterNumber = 6;
	set typeOfFirstFixedSurface = 1;
	set typeOfSecondFixedSurface = 8;
	set scaledValueOfSecondFixedSurface = missing();
	set scaleFactorOfSecondFixedSurface = missing();
	set scaledValueOfFirstFixedSurface = missing();
	set scaleFactorOfFirstFixedSurface = missing();
	}

# CIN
if( parameterCategory == 7 && parameterNumber == 7 && discipline == 0 &&
    typeOfFirstFixedSurface == 1 && typeOfSecondFixedSurface == 255 )
	{ 
#	set shortName = 'ci';
	set parameterNumber = 7;
	set typeOfFirstFixedSurface = 1;
        set typeOfSecondFixedSurface = 8;
        set scaledValueOfSecondFixedSurface = missing();
        set scaleFactorOfSecondFixedSurface = missing();
        set scaledValueOfFirstFixedSurface = missing();
        set scaleFactorOfFirstFixedSurface = missing();
	}


if(parameterCategory == 0 && parameterNumber == 0 && typeOfFirstFixedSurface == 1 && discipline == 0)
	{ set tigge_short_name = 'skt'; }

if(parameterCategory == 1 && parameterNumber == 13 && typeOfFirstFixedSurface == 1 && discipline == 0)
	{ set tigge_short_name = 'sd'; }

if(parameterCategory == 1 && parameterNumber == 8 && typeOfFirstFixedSurface == 1 && discipline == 0)
	{ set tigge_short_name = 'tp'; }

if(typeOfSecondFixedSurface == 255)
	{
	set scaledValueOfSecondFixedSurface = missing();
	set scaleFactorOfSecondFixedSurface = missing();
	}

if(typeOfSecondFixedSurface == 8)
	{
	set scaledValueOfSecondFixedSurface = missing();
	set scaleFactorOfSecondFixedSurface = missing();	
	}

if(typeOfFirstFixedSurface == 1)
	{
	set scaledValueOfFirstFixedSurface = missing();
	set scaleFactorOfFirstFixedSurface = missing();
	}

if(typeOfFirstFixedSurface == 101)
	{
	set scaledValueOfFirstFixedSurface = missing();
	set scaleFactorOfFirstFixedSurface = missing();
	}



# ncdcTigge C program produces 11 records, prevents grib_filter from creating duplicates
switch  ( tigge_short_name )
        {
        case "pt":    print "ncdcTigge param pt ignored";
        case "sf":    print "ncdcTigge param sf ignored";
        case "sm":    print "ncdcTigge param sm ignored";
        case "st":    print "ncdcTigge param st ignored";
        case "ttr":   print "ncdcTigge param ttr ignored";
        case "slhf":  print "ncdcTigge param slhf ignored";
        case "ssr":   print "ncdcTigge param ssr ignored";
        case "str":   print "ncdcTigge param str ignored";
        case "sshf":  print "ncdcTigge param sshf ignored";
        case "tcw":   print "ncdcTigge param tcw ignored";
        case "tp":    print "ncdcTigge param tp removed";
#  exceptions requested by ECMWF on Feb.2018
# o  sfc var t is not desired, other levels are OK 
        case "t":
            if( typeOfFirstFixedSurface != 1 && typeOfFirstFixedSurface != 103 && scaledValueOfFirstFixedSurface != 5000 )
                 { write; }
# o  Only need sfc geo-hgt once from t=0
        case "orog":
            if( forecastTime == 0 )
                 { write; }

# Write all vars that survive beyond this point.
#
        default: 
# Feb.2018  Fix : on the level 50 there should be only geopotential but now there are all parameters
            if( typeOfFirstFixedSurface == 100 && scaledValueOfFirstFixedSurface == 5000 )
                {
                if( parameterCategory == 3 && parameterNumber == 5 )   { write; }
                else { print "EXCEPTION  no 50 mb except HGT"; }
                }
            else { write; }
        }

#print " END ";
